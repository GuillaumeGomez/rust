// All stability badges should have rounded corners and colored backgrounds.
goto: "file://" + |DOC_PATH| + "/test_docs/index.html"
show-text: true

// Checking the alignment of the badges text with the item name.
assert-css: (
    "//*[@class='item-table']//*[@class='item-left module-item']/*[@class='stab deprecated']",
    {"padding-top": "2px"},
)
assert-position: (
    "//*[@class='item-table']//*[@class='item-left module-item']/*[@class='stab deprecated']",
    {"y": 1892},
)
assert-position: (
    "//*[@class='item-table']//*[@class='item-left module-item']/*[@class='stab deprecated']/preceding-sibling::a",
    {"y": 1894}, // 1892 + 2 because of padding
)

define-function: (
	"check-badge",
	(theme, background, color),
	[
        ("local-storage", {"rustdoc-use-system-theme": "false", "rustdoc-theme": |theme|}),
        ("goto", "file://" + |DOC_PATH| + "/test_docs/index.html"),
		("assert", (".docblock .stab")),
		("assert", (".item-table .stab")),
		("assert-css", (".stab", {
			"border-radius": "3px",
			"color": |color|,
			"background-color": |background|,
		})),
        ("goto", "file://" + |DOC_PATH| + "/test_docs/fn.replaced_function.html"),
		("assert", (".item-info .stab")),
		("assert-css", (".stab", {
			"border-radius": "3px",
			"color": |color|,
			"background-color": |background|,
		})),
	]
)

call-function: ("check-badge", {
	"theme": "ayu",
	"color": "rgb(197, 197, 197)",
	"background": "rgb(49, 69, 89)",
})
call-function: ("check-badge", {
	"theme": "dark",
	"color": "rgb(221, 221, 221)",
	"background": "rgb(49, 69, 89)",
})
call-function: ("check-badge", {
	"theme": "light",
	"color": "rgb(0, 0, 0)",
	"background": "rgb(255, 245, 214)",
})
